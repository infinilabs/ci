plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'com.infinilabs'
archivesBaseName = 'easysearch-client'
version = '2.0.2'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:none" << "-nowarn" 
        options.deprecation = false
        options.warnings = false
    }
    
    tasks.withType(Javadoc).configureEach {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
        options.quiet()
        options.encoding = 'UTF-8'
        failOnError = false
    }
}

java {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

def loadVersionProperties() {
    def props = new Properties()
    file('version.properties').withInputStream { stream ->
        props.load(stream)
    }
    return props
}

def versionProps = loadVersionProperties()
versionProps.each { key, value ->
    project.ext.set(key, value)
}

versionProps.each { key, value ->
    project.ext.set(key, value)
}

dependencies {
    def jacksonVersion = "2.15.3"
    def jacksonDatabindVersion = "2.15.3"
    def hamcrestVersion = "2.1"
    def randomizedrunnerVersion = "2.7.1"
    def slf4jVersion = "1.7.36"
    implementation project(':rest')
    testImplementation("org.hamcrest:hamcrest:$hamcrestVersion")
    testImplementation("com.carrotsearch.randomizedtesting:randomizedtesting-runner:$randomizedrunnerVersion") {
        exclude group: "junit"
    }

    api("org.eclipse.parsson:parsson:1.1.7")
    api("com.google.code.findbugs:jsr305:3.0.2")
    api("jakarta.json:jakarta.json-api:2.0.1")

    implementation 'jakarta.json.bind:jakarta.json.bind-api:2.0.0'

    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    
    testImplementation "junit:junit:${junit}"
    testImplementation "org.apache.logging.log4j:log4j-api:${log4j}"
    testImplementation "org.apache.logging.log4j:log4j-core:${log4j}"

}

task generateBundleZip(type: Zip) {
    from shadowJar.archiveFile
    into('lib') {
        from configurations.runtimeClasspath
    }
}

task generateDistributionZip(type: Zip) {
    dependsOn publish

    def archiveFileName = "${archivesBaseName}-${version}.zip"
    destinationDirectory = file("$buildDir/release")
    from "$buildDir/repo"
    exclude("**/maven-metadata*.*")
    doLast {
        println "Finish Generating distribution zip file $archiveFileName"
    }
}


Set<String> excludedDependencies = new HashSet<>()
shadowJar {
    archiveBaseName.set('easysearch-client')
    dependencies {
        exclude {
            boolean isExcluded = it.moduleGroup != 'org.easysearch.client' && it.moduleGroup != 'com.infinilabs'
            if (isExcluded) {
                String depStr = "${it.moduleGroup}:${it.moduleName}:${it.moduleVersion}"
                excludedDependencies.add(depStr)
            }
            return isExcluded
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
            artifact shadowJar.archiveFile
            pom.withXml { xml ->
                def dependenciesNode = xml.asNode().appendNode('dependencies')
                excludedDependencies.each { depStr ->
                    def (group, name, version) = depStr.split(':')
                    if (name.contains('bom')) {
                        dependenciesNode.appendNode('dependency').with {
                            appendNode('groupId', group)
                            appendNode('artifactId', name)
                            appendNode('version', version)
                            appendNode('type', 'pom')
                            appendNode('scope', 'import')
                        }
                        println "dependencies with bom $group:$name:$version"
                    } else {
                        dependenciesNode.appendNode('dependency').with {
                            appendNode('groupId', group)
                            appendNode('artifactId', name)
                            appendNode('version', version)
                            appendNode('scope', 'compile')
                        }
                        println "dependencies with jar $group:$name:$version"
                    }
                }
            }

            pom {
                name = "Easysearch java client"
                description = "Easysearch java client"
                url = "https://github.com/infinilabs/easysearch-client"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "INFINI Labs"
                        url = "https://www.infinilabs.com/"
                        inceptionYear = "2024"
                    }
                }
                scm {
                    url = "git@github.com:infinilabs/easysearch-client.git"
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'localRepo'
            url = "file://${buildDir}/repo"
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publishMavenJavaPublicationToStagingRepository") }
    
    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    
    sign publishing.publications.mavenJava
}

test {
    include '**/*Test.class'
    include '**/*Tests.class'
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}